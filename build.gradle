plugins {
    id 'java'
    id 'net.minecraftforge.gradle.forge' version '2.3-SNAPSHOT'
    id 'com.wynprice.cursemaven' version '2.1.4'
}

group = 'dev.sapphic'
version = '0.1.0'
sourceCompatibility = 1.8

def loadingPlugin = 'dev.sapphic.backpackbaubles.asm.LoadingPlugin'

sourceSets {
    core {
        java.srcDir file('src/core/java')
        resources.srcDir file('src/core/resources')
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
    main {
        runtimeClasspath += core.output
    }
}

task coreJar(type: Jar) {
    from sourceSets.core.allJava
    classifier = 'core'
}

if (hasProperty('signing.mods.keystore')) {
    apply from: 'signing.gradle'
}

minecraft {
    version = '1.12.2-14.23.5.2847'
    mappings = 'stable_39'
    runDir = 'run'
    clientJvmArgs = ["-Dfml.coreMods.load=$loadingPlugin"]
    serverJvmArgs = ["-Dfml.coreMods.load=$loadingPlugin"]
    replaceIn 'BackpackBaubles.java'
    replace '$version': project.version
    if (project.hasProperty('signing.mods.keystore')) {
        replace '$fingerprint': fingerprint
    }
}

repositories {
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.blamejared.com' }
}

dependencies {
    implementation 'org.jetbrains:annotations:19.0.0'
    implementation 'org.checkerframework:checker-qual:3.4.0'
    deobfCompile 'curse.maven:baubles:2518667'
    deobfCompile 'curse.maven:quark:2924091'
    deobfCompile 'curse.maven:autoreglib:2746011'
    deobfCompile 'curse.maven:quark-oddities:2604798'
}

compileJava {
    sourceCompatibility = 1.8
    options.fork = true
    options.verbose = true
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:all'
}

coreJar {
    manifest.attributes FMLCorePlugin: loadingPlugin
}

artifacts {
    archives coreJar
}

jar {
    dependsOn coreJar
    manifest.attributes ContainedDeps: coreJar.archivePath.name
    from(coreJar.archivePath) {
        include '*'
        into 'META-INF/libraries'
    }
}
