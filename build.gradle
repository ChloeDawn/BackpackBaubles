plugins {
    id 'java'
    id 'net.minecraftforge.gradle.forge' version '2.3-SNAPSHOT'
    id 'com.wynprice.cursemaven' version '2.1.4'
}

group = 'dev.sapphic'
version = '0.1.0'
sourceCompatibility = 1.8

minecraft {
    version = '1.12.2-14.23.5.2847'
    mappings = 'stable_39'
    runDir = 'run'
    coreMod = 'dev.sapphic.backpackbaubles.BackpackBaublesPlugin'
    replaceIn 'src/main/java/dev/sapphic/backpackbaubles/BackpackBaubles.java'
    replaceIn 'src/main/java/dev/sapphic/backpackbaubles/BackpackBaublesPlugin.java'
    replace '$version': project.version, '$fingerprint': 'FINGERPRINT' // FIXME
}

repositories {
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.blamejared.com' }
}

dependencies {
    implementation 'org.jetbrains:annotations:19.0.0'
    implementation 'org.checkerframework:checker-qual:3.3.0'
    deobfCompile 'curse.maven:baubles:2518667'
    deobfCompile 'curse.maven:quark:2924091'
    deobfCompile 'curse.maven:autoreglib:2746011'
    deobfCompile 'curse.maven:quark-oddities:2604798'
}

compileJava {
    sourceCompatibility = 1.8
    options.fork = true
    options.verbose = true
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:all'
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf { project.hasProperty 'keyStore' }

    alias = properties['forgegradle.keystore.alias']
    storePass = properties['forgegradle.keystore.pass']
    keyPass = properties['forgegradle.keystore.keypass']
    keyStore = properties['forgegradle.keystore']
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}
